buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.15.0"
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'antlr'

intellij {
    version = ideaVersion

    pluginName 'jetbrains-plugin-st4'
    downloadSources true
    updateSinceUntilBuild false
}

group 'antlr'
version pluginVersion

wrapper {
    gradleVersion = "6.0.1"
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
sourceSets {
    main.antlr.includes = ["ST*.g4"]
}
repositories {
    mavenCentral()
}

// See https://github.com/gradle/gradle/issues/820#issuecomment-288838412
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr("org.antlr:antlr4:$antlr4Version") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    implementation "org.antlr:antlr4-intellij-adaptor:0.1"
    implementation "org.antlr:antlr4-runtime:$antlr4Version"
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.6.28'
}

generateGrammarSource {
    arguments += [
            "-package", "org.antlr.jetbrains.st4plugin.parsing",
            "-long-messages",
            "-no-visitor",
            "-no-listener",
            "-lib","src/main/antlr"
    ]
}
